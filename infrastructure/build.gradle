import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'

    id 'org.springframework.boot' version '2.7.8'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
    id 'org.openapi.generator' version '6.0.1'
}

repositories {
    mavenCentral()
}

//dependencyManagement {
//    imports {
//        mavenBom SpringBootPlugin.BOM_COORDINATES
//    }
//}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
//    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
//    implementation 'io.swagger.core.v3:swagger-annotations:2.2.7'
//    implementation 'io.swagger.core.v3:swagger-models:2.2.7'

    implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.13'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.13'
    implementation 'org.springdoc:springdoc-openapi-kotlin:1.6.13'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation project(':domain:card-transaction')
}

bootJar {
    enabled = false
}

sourceSets {
    main {
        kotlin.srcDirs += "$buildDir/generated/open-api/server/src/main/kotlin"
    }
}

openApiGenerate {
    generatorName = 'kotlin-spring'
    library = 'spring-boot'
    inputSpec = "$rootDir/infrastructure/src/main/resources/open-api/server/card-transaction.yaml" as String
    outputDir = "$buildDir/generated/open-api/server" as String
    packageName = 'org.digilinq.payment.eft.generated.v1'
    apiPackage = 'org.digilinq.payment.eft.generated.v1.api'
    modelPackage = 'org.digilinq.payment.eft.generated.v1.model'
    invokerPackage = 'org.digilinq.payment.eft.generated.v1.invoker'
    enablePostProcessFile = true
    skipOverwrite = false
    configOptions = [
            interfaceOnly       : 'true',
            enumPropertyNaming  : "original",
            serializationLibrary: 'jackson',
            dateLibrary         : 'java8'
    ]
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

compileKotlin.dependsOn tasks.openApiGenerate
compileTestKotlin.dependsOn tasks.openApiGenerate
